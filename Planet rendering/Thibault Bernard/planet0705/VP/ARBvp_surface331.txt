!!ARBvp1.0 
# transformation + lighting + fog + texture matrix
ATTRIB nrml = vertex.position;
ATTRIB nrml2 = vertex.attrib[15];
ATTRIB dir = vertex.texcoord[0];
ATTRIB vtex1 = vertex.texcoord[1];

OUTPUT oPos = result.position;
OUTPUT oCol0 = result.color.primary;
OUTPUT oCol1 = result.color.secondary;
OUTPUT oTex0 = result.texcoord[0];
OUTPUT oTex1 = result.texcoord[1];
OUTPUT oTex2 = result.texcoord[2];
OUTPUT oTex3 = result.texcoord[3];
OUTPUT oTex4 = result.texcoord[4];
OUTPUT oTex5 = result.texcoord[5];

PARAM mtrans[4] = { state.matrix.modelview };
PARAM mproj[4] = { state.matrix.projection };
PARAM mrot[4] = { state.matrix.program[0] };
PARAM mrotinv[4] = { state.matrix.program[1] };
PARAM mtex[4] = { state.matrix.texture[0].invtrans };
PARAM lightdir = program.local[0];
PARAM hazecol = program.local[1];
PARAM fogparams = program.local[2];
PARAM texscale = { 32, 32, 32, 1 };

PARAM yaxis = { 0,1,0,1 };
PARAM nbr = { 1.1, 0.5, 0.0, 1.0 };

TEMP R0,R1,R2,R3,tangent,binormal;

# transformation
# normales & hauteurs
MOV R0,nrml2;
ADD R1, nrml, -R0;
MAD R1, dir.w, R1, R0;	# dir.w = weight

MOV R0,dir;
MUL R2,R0,R1.w;	# R4
MOV R2.w,fogparams.w;
DP4 R0.x,R2,mtrans[0];
DP4 R0.y,R2,mtrans[1];
DP4 R0.z,R2,mtrans[2];
DP4 R0.w,R2,mtrans[3];
DP4 oPos.x,R0,mproj[0];
DP4 oPos.y,R0,mproj[1];
DP4 oPos.z,R0,mproj[2];
DP4 oPos.w,R0,mproj[3];

# eye vector
DP4 R3.x,R2,mrotinv[0];
DP4 R3.y,R2,mrotinv[1];
DP4 R3.z,R2,mrotinv[2];
DP3 R3.w,R3,R3;
RSQ R3.w,R3.w;
MUL R3,R3,R3.w;
MOV oTex3.w,R3.x;
MOV oTex4.w,R3.y;
MOV oTex5.w,R3.z;

# bump lighting
DP3 R2.x,R1,mrot[0]; # R1 was R2
DP3 R2.y,R1,mrot[1];
DP3 R2.z,R1,mrot[2];

XPD tangent,R2,yaxis;
XPD binormal,R2,tangent;

MOV oTex3.x,tangent.x;
MOV oTex3.y,binormal.x;
MOV oTex3.z,R2.x;

MOV oTex4.x,tangent.y;
MOV oTex4.y,binormal.y;
MOV oTex4.z,R2.y;

MOV oTex5.x,tangent.z;
MOV oTex5.y,binormal.z;
MOV oTex5.z,R2.z;

# daylight
DP4 R3.x,-dir,mrot[0];
DP4 R3.y,-dir,mrot[1];
DP4 R3.z,-dir,mrot[2];
DP4 R3.w,dir,mrot[3];
DP3 R2,lightdir,R3;

# fog
DP3 R1.w,R0,R0;
RSQ R0.w,R1.w;
MUL R1.w,R0.w,R1.w;
ADD R1.w,fogparams.y,R1.w;
MUL R1.w,fogparams.x,R1.w;
MAX R2.w,fogparams.z,R2.w;
MOV oTex2.w,R1.w; # densité
MOV oTex1.w,R2.w; # luminance

# color maps
DP3 R0.x,dir,mtex[0];
DP3 R0.y,dir,mtex[1];
DP3 R0.z,dir,mtex[2];
MOV oTex1.xy,vtex1;
MUL oTex2.xy,vtex1,texscale;
MOV oTex0,R0;

END
